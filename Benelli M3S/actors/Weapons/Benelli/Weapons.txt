ACTOR BenelliM3S : Weapon replaces Shotgun
{
 //$Category weapons
 Scale 0.28
 Weapon.SelectionOrder 1000
 Weapon.AmmoType "ShotClip"
 Weapon.AmmoGive 6
 Weapon.AmmoUse	1
 Weapon.AmmoType2 "ShotShell"
 Weapon.AmmoUse2 0
 Weapon.AmmoGive2 6
 Weapon.SlotNumber 3
 Weapon.BobStyle "Alpha"
 AttackSound "sounds/weapons/benellim3s/"
 Inventory.Pickupmessage ""
 Inventory.Amount 1
 Inventory.MaxAmount 1
 +AMMO_OPTIONAL
 +NOAUTOFIRE
 +NOEXTREMEDEATH
 +IGNORESKILL
 States
 {
 Ready:
  BENE A 1 A_WeaponReady(WRF_ALLOWRELOAD)
  TNT1 A 0 A_SetCrosshair(2)
  Loop
 Deselect:
  BENE A 1 A_PlaySound("BUNEQUIP",1,1)
 DeselectLoop:
  BENE A 1 A_Lower
  Goto DeselectLoop
 Select:
  BENE A 1 A_PlaySound("BEQUIP",1,1)
  BENE A 1 A_Raise
  Goto Select+1	
 Fire:
  BENE A 0 A_JumpIfNoAmmo("DryFire")
  BENE A 1 A_PlaySound("SGSHOT1",CHAN_WEAPON,1)
  BENE B 0 A_SetPitch(pitch+(random(1,4)))
  BENE B 0 A_SetPitch(pitch-(random(1,6)))
  BENE B 0 A_Recoil(1.0)
  BENE B 0 A_GunFlash
  BENE B 0 A_Quake(3,6,0,6) //1
  BENE C 0 A_FireBullets(7,3,6,7,"BulletPuff",1) //1
  BENE D 0 //1
  //BENE D 0 A_SpawnDebris("12gdebris")
  BENE D 0 A_SetPitch(pitch+0.25)
  BENE D 0 A_SetPitch(pitch+0.25)
  BENE E 1 A_FireCustomMissile("BenelliSmokeSpawn",0,0,6,8)
  BENE F 1
  BENE GHIJK 1
  BENE L 6
  BENE A 1 A_WeaponReady
  Goto Ready
 DryFire:
  BENE A 1 Offset(1,30) A_PlaySound("DRYFIRE",1,1)
  BENE B 1 Offset(1,31)
  BENE B 1 Offset(1,32)
  BENE B 1 Offset(1,33)
  BENE A 3
  Goto Ready 
 Reload:
  TNT1 A 0 A_JumpIfInventory("ShotClip",6,2) //Do nothing if full mag capacity
  TNT1 A 0 A_JumpIfInventory("ShotShell",1,2)
  BENE A 1
 Goto Ready
  BENR B 1 Offset(1,30) A_PlaySound("COCK",0,1)
  BENR C 1 Offset(2,31)
  BENR D 1 Offset(3,32)
  BENR E 1 Offset(4,33)
  BENR F 4 Offset(5,34)
  Goto ReloadWork
 ReloadWork:
  BENR F 0 A_TakeInventory("ShotShell",1)
  BENR F 0 A_GiveInventory("ShotClip",1)
  BENR GHIJKL 1 Offset(5,34)
  BENR M 2 Offset(4,35)
  BENR N 2 Offset(2,36)
  BENR O 4 Offset(0,37) A_PlaySound("INSERT",1,1)
  BENR P 2 Offset(2,36)
  BENR Q 2 Offset(4,35)
  BENR R 1 Offset(5,34)
  BENR STUV 1 Offset(5,34)
  BENR FFFF 1 A_WeaponReady
  TNT1 A 0 A_JumpIfInventory("ShotClip",6,"ReloadFinish")
  TNT1 A 0 A_JumpIfInventory("ShotShell",1,"ReloadWork")
 ReloadFinish:
  BENR F 1 Offset(5,34) A_PlaySound("UNCOCK",0,1)
  BENR E 1 Offset(4,33)
  BENR D 1 Offset(3,32)
  BENR C 1 Offset(2,31)
  BENR B 1 Offset(1,30)
  Goto Ready
 Flash:
  BENF M 1 Bright A_Light(20)
  BENF N 1 Bright A_Light(10)
  Goto LightDone
 Spawn:
  BENE Z -1
  Stop
 }
}

ACTOR ShotShell : Ammo replaces Shell
{
	//$Category ammunition
	Inventory.MaxAmount 150
	Inventory.PickupMessage "You got some 12 gauge shells"
	Inventory.Amount 4
	Inventory.PickupSound "SGPUMP"
	Inventory.Icon "Shell1"
	Ammo.DropAmount 4
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 150
	Scale 0.75
	-DROPPED
	+IGNORESKILL
	States
	{
	Spawn:
	    	SHEL A -1
	    	STOP
	}
}

ACTOR ShotClip : Ammo 30001
{
	Inventory.MaxAmount 6
	Inventory.Icon "Shell1"
	-DROPPED
	+IGNORESKILL
}

ACTOR ShotShellBox : ShotShell replaces ShellBox
{
	Inventory.PickupMessage "You got a box of 12 gauge shells."
    Inventory.Amount 12
	-DROPPED
	-IGNORESKILL
    States
    {
      Spawn:
      SBOX A -1
      Stop
  }
}

/*
ACTOR 12gdebris
{
  Health 1
  Scale 0.3
  Radius 1
  Height 1
  AttackSound "sounds/weapons/benellim3s/"
  +FORCEYBILLBOARD
  +DOOMBOUNCE
  +DONTSPLASH
  -SOLID
  States
  {
  Spawn:
    TNT1 A 1
    BRS5 ABCDEFGHIJKL 1
	BRS5 M 1 A_PlaySound("12GSH",1,1)
	BRS5 NOPQRST 1
	BRS5 I -1
    Loop
  }
}
*/

ACTOR BenelliSmokeSpawn
{
    Radius 1
    Height 1
    Speed 30
    PROJECTILE
    States
    {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        Goto Death
    Death:
        TNT1 A 0 A_SpawnProjectile("BenelliSmoke",-6,4,0)
        Stop
    }
}

ACTOR BenelliSmoke
{
	Radius		1
	Height		1
	RenderStyle ADD
	Alpha		.6
	Scale		.10
	+NOGRAVITY
	+NOBLOCKMAP
	+FLOORCLIP
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1 
		Stop
	}
}

ACTOR BenelliDummy : CustomInventory //This replaces Shotgun drops so that you always get 4 shells when picking up a Benelli.
{
	Inventory.PickupSound "SGPUMP"
	Inventory.PickupMessage ""
	Scale 0.28

    States
    {
    Spawn:
        BENE Z -1
        Stop

    Pickup:
        // If you have a shotgun, go to HaveShotgun state, and give four shells.
        // If not, give shotgun.
        TNT1 A 0 A_JumpIfInventory("BenelliM3S", 1, "HaveShotgun")
        TNT1 A 0 A_GiveInventory("BenelliM3S")
		TNT1 A 0 A_Log("\cGYou Got the Benelli M3S.")
        Stop

    HaveShotgun:
        TNT1 A 0 A_GiveInventory("ShotShell", 4)
		TNT1 A 0 A_Log("\cGYou empty the Benelli M3S.")
        Stop
    }
}